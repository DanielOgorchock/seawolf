
import distutils.sysconfig

env = Environment()
env['STATIC_AND_SHARED_OBJECTS_ARE_THE_SAME']=True

env.ParseConfig("pkg-config --cflags --libs opencv")
env.Append(CFLAGS = "-std=c99")
env.Append(CFLAGS = "-g")

# Objects created from src/*.c
lib_objects = env.SharedObject(
    source = Glob("src/*.c"),
    CPPPATH = ["include/"] + env["CPPPATH"],
)

# libvision.so
# This is the library for the c part of the vision library.  It can be used
# without the python portion of the library if you want.
env.SharedLibrary(
    target = "libvision",
    source = lib_objects,
)

# interface_wrap.c
# Wrapper for all c code.  Generated by swig.
swig_command = env.Command(
    "interface_wrap.c",
    "interface.i",
    "swig -python -o $TARGET $SOURCE",
)
Clean(swig_command, "interface.py")

# interface.so
# This is a python extension module generated by swig and used by interface.py
env.SharedLibrary(
    target = "_interface",
    SHLIBPREFIX = "",
    source = ["interface_wrap.c", lib_objects],
    CPPPATH = [
        "include/",
        distutils.sysconfig.get_python_inc(),
    ] + env["CPPPATH"]
)
